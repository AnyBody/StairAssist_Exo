Main.ExoV2.Right = {
  //********************Draw Settings for Contact Nodes on Exo**********************
  AnyFolder ContactNodesDrawSettings = {
    AnyFloat RGB = {0.2, 0.2, 1.0};
    AnyVar Opacity = 1;
    AnyFloat ScaleXYZ = 5*{0.003440291092, 0.003440291092, 0.003440291092};
    AnySwitchVar Visible = Off;
    AnySwitchVar VisibleKeyNodes = Off;
  };
  //********************************************************************************
  
  // Contact nodes must be oriented normal to contact surface for the conditional 
  // contact class to work. Nodes are arranged in vertical lines that run across the
  // contact surface of the exoskeleton (except foot base, where GRF prediction is used).
  
  // Contact planes contain reference frames that are needed for the orientation of the 
  // contact nodes. ARel of each Contact plane is referred by contact nodes in the same
  // vertical line (relative to exo). Each node is oriented with z axis normal to the 
  // surface (pointing inwards), y axis parallel to the length of the part (vertical line)
  // and pointing upwards. IN SOME CASES, Y AXIS IS NOT ALWAYS PARALLEL TO THE LENGTH
  
  Foot_Segment___1 = {
    // Grid of 3 (vertical) x 6 (curved) = 18 nodes (on the strap).
    // Vertical distance between nodes = 2.4 - 2.5 cm
    // Distance between vertical lines = 1.7 - 2.3 cm
    
    #include "RFoot_ConNodes.any"
    
    // Make the GRF act on the exo foot instead of the human foot.
    // First we make nodes for heel contact toe lateral contact like in the foot.
    // These nodes are used in the class template of ForcePlateAutoDetection
    // See #include "../../ForcePlates/ForcePlateAutoDetection.any"
    // Also See #include "<ANYBODY_PATH_MAINFILEDIR>/Setup/ForcePlates.any"
    
    AnyRefNode HeelContactNodeLow = {
      sRel = {.CS_CenterBase.sRel[0] ,
              .CS_CenterBase.sRel[1] - 0.156,  // At the edge of the foot along the foot length
              .CS_CenterBase.sRel[2] - 0.008}; // below the surface of the exo foot (3mm thickness + 5mm extra) 
      ARel = .CS_CenterBase.ARel;
      AnyDrawRefFrame drw = {Visible = ...ContactNodesDrawSettings.VisibleKeyNodes;};
    };
    AnyRefNode ToeLateralContactNode = {
      sRel = {.CS_CenterBase.sRel[0] + 0.033,  // shift laterally at the outer edge of the exo foot
              .CS_CenterBase.sRel[1] + 0.096,  // shift forward
              .CS_CenterBase.sRel[2] - 0.008};  // shift downwards below the surface of the exo foot (3mm thickness + 5mm extra)
      ARel = .CS_CenterBase.ARel;
      AnyDrawRefFrame drw ={Visible = ...ContactNodesDrawSettings.VisibleKeyNodes;};
    };    
    AnyRefNode GroundJointRotated = {
      sRel = .CS_CenterBase.sRel;
      ARel = .CS_CenterBase.ARel;
      AnyDrawRefFrame drw = {Visible = ...ContactNodesDrawSettings.VisibleKeyNodes;};
    };        
    
  }; // Foot
  
  Shank_Segment___1 = {
    // Total nodes = 118 nodes.
    // Vertical distance between nodes = 1.7 - 1.9 cm on surfaces
    // Vertical distance between nodes = 1.5 cm on strap
    // Distance between vertical lines = 1.3 - 2.0 cm
    // Variable distance between vertical lines due to non cylindrical
    // geometry.
    // Nodes on shank main: Node 001 to Node 056.
    // Nodes on shank medial: Node 071 to Node 118.
    // Nodes on shank strap: Node 057 to Node 070.
    #include "RShank_ConPlanes.any"
    #include "RShank_ConNodes.any"
    
    ShankAxis = {
      AnyRefNode CenNode = {
        sRel = {0, 0, 0.200/2}; 
        ARel = RotMat(pi,y);
        AnyDrawRefFrame drw ={Visible = ....ContactNodesDrawSettings.VisibleKeyNodes;};
      };
      AnyRefNode CenNodeShankFront = {
        sRel = {-0.065, 0, 0.200/2}; 
        ARel = RotMat(pi,y);
        AnyDrawRefFrame drw ={Visible = ....ContactNodesDrawSettings.VisibleKeyNodes;};
      };    
    };
    
  }; // Shank
  
  Thigh_Segment___1 = {
    // Total nodes = 120 nodes.
    // Vertical distance between nodes = 1.7 - 1.8 cm on surfaces
    // Vertical distance between nodes = NA. Single Node on strap (width = 3.8 cm)
    // Distance between vertical lines = 2.0 - 2.6 cm
    // Variable distance between vertical lines due to conical geometry.
    // Nodes on thigh main: Node 001 to Node 072.
    // Nodes on thigh posterior: Node 073 to Node 108.
    // Nodes on thigh strap: Node 109 to Node 120.
    #include "RThigh_ConPlanes.any"
    #include "RThigh_ConNodes.any"
    
    ThighAxis = {
      AnyRefNode CenNode = {
        sRel = {0, 0, 0.175/2};     
        //ARel = RotMat(pi,x);
        AnyDrawRefFrame drw ={Visible = ....ContactNodesDrawSettings.VisibleKeyNodes;};
      };
    };
    
  }; // Thigh Segment
  
  Foot_Rail___1 = {
    CS_AnkleJoint = {
      AnyRefNode AnkleJointRotated = {
        ARel = RotMat(pi,y) * RotMat(-pi/2,x); 
        AnyDrawRefFrame drw = {RGB={0,0.6,0.8}; ScaleXYZ={0.3,0.3,0.3};
          Visible = ....ContactNodesDrawSettings.VisibleKeyNodes;}; 
      };
    };
  }; // Foot Rail
  
  Shank_Upper_Rail___1 = {
    CS_KneeJoint = {
      AnyRefNode KneeJointRotated = {
        ARel = RotMat(pi,y) * RotMat(-pi/2,x); 
        AnyDrawRefFrame drw = {RGB={0,0.6,0.8}; ScaleXYZ={0.3,0.3,0.3};
          Visible = ....ContactNodesDrawSettings.VisibleKeyNodes;}; 
      };
    };
  }; // Shank Upper Rail
  
}; // Exo Right

